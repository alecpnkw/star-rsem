from snakemake.utils import min_version
import pandas as pd 
from os.path import dirname, basename
min_version("8.0")

configfile: "config/config.yaml"
localrules: all, multiqc

# Todo: add schema validation to config, samples files...
# Expect space-delimited samples.txt with columns: SampleID Condition Replicate
samples = pd.read_csv(config["samples"], sep=' ', header=0).set_index("SampleID", drop=False)

# targeting rule
rule all:
    input:
        expand(["results/rsem-combined/rsem_transcript_{genome}.tsv",
        "results/rsem-combined/rsem_gene_{genome}.tsv"], genome = config["genome"]["build"]),
        expand("results/fastqc/{sample}_{read}.html", sample=samples.index, read = ["R1", "R2"]),
        "results/multiqc/multiqc-report.html"

# including last rule here... 
rule multiqc:
    input:
        expand("results/rsem/{sample}_{genome}/{sample}_{genome}.isoforms.results", sample=samples.index, genome = config["genome"]["build"]),
        expand("results/fastqc/{sample}_{read}.html", sample=samples.index, read = ["R1", "R2"])
    output:
        "results/multiqc/multiqc-report.html"
    conda: 
        "../envs/multiqc.yaml"
    shell:
        """
        multiqc \
        --no-data-dir \
        --filename {output} \
        results/
        """
        
include: "rules/fastqc.smk"
include: "rules/star.smk"
include: "rules/rsem.smk"
